pipeline StopPipeline {
    constraint Zone2001 oftype RangeConstraint {
        lowerBound: 2001;
        lowerBoundInclusive: true;
        upperBound: 2001;
        upperBoundInclusive: true;}
    valuetype FilteredZone oftype integer {
        constraints: [Zone2001];}

    constraint ValidGeoCoordiante oftype RangeConstraint {
        lowerBound: -90;
        lowerBoundInclusive: true;
        upperBound: 90;
        upperBoundInclusive: true; }
    valuetype GeographicCoordiante oftype decimal {
        constraints: [ValidGeoCoordiante];}

    block DataExtractor oftype HttpExtractor {
        url: "https://gtfs.rhoenenergie-bus.de/GTFS.zip";}

    block ZipInterpreter oftype ArchiveInterpreter {
        archiveType: "zip"; }

    block FilePicker oftype FilePicker {
        path: "/stops.txt";}

    block TextFileInterpreter oftype TextFileInterpreter {
        encoding: "utf8";}

    block DataCSVInterpreter oftype CSVInterpreter {
        delimiter: ",";
        enclosing: '"';}

    block DataTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "stop_id" oftype integer,
            "stop_name" oftype text,
            "stop_lat" oftype GeographicCoordiante,
            "stop_lon" oftype GeographicCoordiante,
            "zone_id" oftype FilteredZone,
        ];}

    block DataLoader oftype SQLiteLoader {
        table: "stops";
        file: "./gtfs.sqlite";}

    DataExtractor
        -> ZipInterpreter
        -> FilePicker
        -> TextFileInterpreter
        -> DataCSVInterpreter
        -> DataTableInterpreter
        -> DataLoader;
}